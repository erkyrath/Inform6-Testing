Constant Story "IfdefVNTest";
Constant Headline "Not a game.^";

Release 1;

! This is a compiler unit test for the "#ifdef VN_nnnn" construct

#ifdef TARGET_ZCODE;
Constant HDR_GAMERELEASE      = $02;     ! word
Constant HDR_GAMESERIAL       = $12;     ! six ASCII characters
#ifnot;
Global gg_mainwin;
Constant HDR_GLULXVERSION  $04;     ! long word
Constant ROM_GAMERELEASE   $34;     ! short word
Constant ROM_GAMESERIAL    $36;     ! six ASCII characters
#endif; ! TARGET_GLULX

Global failures;

[ Main;
    #ifdef TARGET_GLULX;
	@setiosys 2 0;
	@push 201; @push 3; @push 0; @push 0; @push 0;
	@glk $0023 5 gg_mainwin;

	@push gg_mainwin;
	@glk $002F 1 0;
    #endif; ! TARGET_GLULX

	Banner();
	new_line;
	RunTest();
];

[ Banner  ix;
    if (Story ~= 0) {
        #ifdef TARGET_ZCODE;
        #ifV5; style bold; #Endif;
        print (string) Story;
        #ifV5; style roman; #Endif;
        #ifnot; ! TARGET_GLULX;
        glk($0086, 3); ! set header style
        print (string) Story;
        glk($0086, 0); ! set normal style
        #Endif; ! TARGET_
    }
    if (Headline ~= 0) print ": ", (string) Headline;
    #ifdef TARGET_ZCODE;
    print "Release ", (HDR_GAMERELEASE-->0) & $03ff, " / Serial number ";
    for (ix=0 : ix<6 : ix++) print (char) HDR_GAMESERIAL->ix;
    #ifnot; ! TARGET_GLULX;
    print "Release ";
    @aloads ROM_GAMERELEASE 0 ix;
    print ix;
    print " / Serial number ";
    for (ix=0 : ix<6 : ix++) print (char) ROM_GAMESERIAL->ix;
    #Endif; ! TARGET_
    print " / Inform v"; inversion;
    print ", compiler options ";
    ix = false;
    #ifdef STRICT_MODE;
    print "S"; ix++;
    #Endif; ! STRICT_MODE
    #ifdef INFIX;
    print "X"; ix++;
    #ifnot;
    #ifdef DEBUG;
    print "D"; ix++;
    #Endif; ! DEBUG
    #Endif; ! INFIX
    if (~~ix) print "(none)";
    new_line;

    #ifdef TARGET_GLULX;
    @gestalt 1 0 ix;
    print "Interpreter version ", ix / $10000, ".", (ix & $FF00) / $100,
    ".", ix & $FF, " / ";
    @gestalt 0 0 ix;
    print "VM ", ix / $10000, ".", (ix & $FF00) / $100, ".", ix & $FF, " / ";
    ix = HDR_GLULXVERSION-->0;
    print "game file format ", ix / $10000, ".", (ix & $FF00) / $100, ".", ix & $FF, "^";
    #Endif; ! TARGET_GLULX
];

[ RunTest;
	RunAllTests();

	new_line;
	if (failures == 0)
		print "All passed.^";
	else
		print failures, " errors!^";
];

[ RunAllTests;
	#ifdef VN_1620;
	print "VN_1620: yes^";
	#ifnot;
	print "VN_1620: no: (ERROR)^"; failures++;
	#endif;

	#ifndef VN_1620;
	print "VN_1620: no: (ERROR)^"; failures++;
	#ifnot;
	print "VN_1620: yes^";
	#endif;

	! We'll have to fix this test when Inform 6.99 is released!
	#ifdef VN_1699;
	print "VN_1699: yes: (ERROR)^"; failures++;
	#ifnot;
	print "VN_1699: no^";
	#endif;
	
	#ifndef VN_1699;
	print "VN_1699: no^";
	#ifnot;
	print "VN_1699: yes: (ERROR)^"; failures++;
	#endif;

	! The following should be treated as regular constants, not magic Inform-version constants.
	
	#ifdef VN_100;
	print "VN_100: yes: (ERROR)^"; failures++;
	#ifnot;
	print "VN_100: no^";
	#endif;

	#ifdef VN_10000;
	print "VN_10000: yes: (ERROR)^"; failures++;
	#ifnot;
	print "VN_10000: no^";
	#endif;

	#ifdef VN_100X;
	print "VN_100X: yes: (ERROR)^"; failures++;
	#ifnot;
	print "VN_100X: no^";
	#endif;

	#ifdef VN_XXXX;
	print "VN_XXXX: yes: (ERROR)^"; failures++;
	#ifnot;
	print "VN_XXXX: no^";
	#endif;

];
