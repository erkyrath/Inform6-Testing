! Test various valid and invalid uses of the Global and Array directives.

[ Main;
	Func();
	
	Func2(); ! no effect, just suppressing a warning
];

! The equals sign is now optional for both Constant and Global.

Constant c1 11;
Constant c2 = 22;
Global g1 33;
Global g2 = 44;

[ Func;
	print "c1=", c1, " (11)^";
	print "c2=", c2, " (22)^";
	print "g1=", g1, " (33)^";
	print "g2=", g2, " (44)^";

	g1 = 333;
	g2 = 444;
	print "g1=", g1, " (333)^";
	print "g2=", g2, " (443)^";

	print "arr-->0=", arr-->0, " (12)^";
	print "arr-->1=", arr-->1, " (13)^";
	print "arr-->2=", arr-->2, " (14)^";
	print "arr-->3=", arr-->3, " (15)^";
];

Array arr --> 12 13 14 15;

#ifdef USE_GLOBAL_BEFORE_DEF;

[ Func1;
	return glob1;
];

! Error:  Variable must be defined before use
Global glob1;

#endif; ! USE_GLOBAL_BEFORE_DEF


[ Func2;
	return array1;
];

! It's okay to define an array after use
Array array1 --> 2;


#ifdef DEFINE_GLOBAL_TWICE;
Global glob2;
! ...is a name already in use and may not be used as a new global variable name
Global glob2;
#endif; ! DEFINE_GLOBAL_TWICE

#ifdef DEFINE_GLOBAL_NONSYMBOL;
! Expected new global variable name but found...
Global 3;
#endif; ! DEFINE_GLOBAL_NONSYMBOL

#ifdef DEFINE_GLOBAL_STATIC;
! Global variables cannot be static
Global glob3 static;
#endif; ! DEFINE_GLOBAL_STATIC


! It's okay to redefine built-in globals
Global self;
Global temp__global2;

! And even to give one a new initial value
Global temp__global1 = 99;


#ifdef DEFINE_GLOBAL_EXTRA;
! No such constant as...
Global glob4 foof;
#endif; ! DEFINE_GLOBAL_EXTRA


#ifdef DEFINE_GLOBAL_NOVALUE;
! Expected expression but found ;
Global glob4 = ;
#endif; ! DEFINE_GLOBAL_NOVALUE


#ifdef DEFINE_GLOBAL_ARRAY;
! use 'Array' to define arrays, not 'Global'
Global glob4 --> 4;
#endif; ! DEFINE_GLOBAL_ARRAY


#ifdef DEFINE_ARRAY_NO_DEF;
! Expected array definition but found ;
Array array2;
#endif; ! DEFINE_ARRAY_NO_DEF


#ifdef DEFINE_ARRAY_EXTRA;
! Expected '->', '-->', 'string', 'table' or 'buffer' but found...
Array array3 foo;
#endif; ! DEFINE_ARRAY_EXTRA


