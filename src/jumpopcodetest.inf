Constant Story "JumpOpcodeTest";
Constant Headline "Not a game.^";

Release 1;

! This is a compiler unit test for the @jump opcode.

#ifdef TARGET_ZCODE;
Constant HDR_GAMERELEASE      = $02;     ! word
Constant HDR_GAMESERIAL       = $12;     ! six ASCII characters
#ifnot;
Global gg_mainwin;
Constant HDR_GLULXVERSION  $04;     ! long word
Constant ROM_GAMERELEASE   $34;     ! short word
Constant ROM_GAMESERIAL    $36;     ! six ASCII characters
#endif; ! TARGET_GLULX

Global failures;

[ Main loc;
    #Ifdef TARGET_GLULX;
	@setiosys 2 0;
	@push 201; @push 3; @push 0; @push 0; @push 0;
	@glk $0023 5 gg_mainwin;

	@push gg_mainwin;
	@glk $002F 1 loc;
    #Endif; ! TARGET_

	loc = 0;

	Banner();
	new_line;
	RunTest();
];

[ Banner  ix;
    if (Story ~= 0) {
        #ifdef TARGET_ZCODE;
        #ifV5; style bold; #Endif;
        print (string) Story;
        #ifV5; style roman; #Endif;
        #ifnot; ! TARGET_GLULX;
        glk($0086, 3); ! set header style
        print (string) Story;
        glk($0086, 0); ! set normal style
        #Endif; ! TARGET_
    }
    if (Headline ~= 0) print ": ", (string) Headline;
    #ifdef TARGET_ZCODE;
    print "Release ", (HDR_GAMERELEASE-->0) & $03ff, " / Serial number ";
    for (ix=0 : ix<6 : ix++) print (char) HDR_GAMESERIAL->ix;
    #ifnot; ! TARGET_GLULX;
    print "Release ";
    @aloads ROM_GAMERELEASE 0 ix;
    print ix;
    print " / Serial number ";
    for (ix=0 : ix<6 : ix++) print (char) ROM_GAMESERIAL->ix;
    #Endif; ! TARGET_
    print " / Inform v"; inversion;
    print ", compiler options ";
    ix = false;
    #ifdef STRICT_MODE;
    print "S"; ix++;
    #Endif; ! STRICT_MODE
    #ifdef INFIX;
    print "X"; ix++;
    #ifnot;
    #ifdef DEBUG;
    print "D"; ix++;
    #Endif; ! DEBUG
    #Endif; ! INFIX
    if (~~ix) print "(none)";
    new_line;

    #ifdef TARGET_GLULX;
    @gestalt 1 0 ix;
    print "Interpreter version ", ix / $10000, ".", (ix & $FF00) / $100,
    ".", ix & $FF, " / ";
    @gestalt 0 0 ix;
    print "VM ", ix / $10000, ".", (ix & $FF00) / $100, ".", ix & $FF, " / ";
    ix = HDR_GLULXVERSION-->0;
    print "game file format ", ix / $10000, ".", (ix & $FF00) / $100, ".", ix & $FF, "^";
    #ifnot;
    print "Game file format ", 0->0, ".^";
    #Endif; ! TARGET_GLULX
];

[ check_value val1 val2;
	print val1;
	if (val1 ~= val2) {
		failures++;
		print " (ERROR, should be ", val2, ")";
	}
];

[ RunTest val;
	val = JumpLooper(0);
	print "JumpLooper(0)="; check_value(val, 7);
	new_line;
	val = JumpLooper(8);
	print "JumpLooper(8)="; check_value(val, 11);
	new_line;
	val = JumpLooper(11);
	print "JumpLooper(11)="; check_value(val, 12);
	new_line;
];

[ JumpLooper val;
	! This tests backwards and forwards jumps.
	! If OPFORM is defined, this uses @jump opcodes. (Which differ slightly
	! in syntax between Z/G, but behave the same.) If OPFORM is not defined,
	! this uses jump *statements*. The function should compile to exactly
	! the same code either way.
	
	val = val + 1;

	if (val > 10) {
		#ifdef OPFORM;
		#ifdef TARGET_ZCODE;
		@jump end;
		#ifnot;
		@jump ?end;
		#endif;
		#ifnot;
		jump end;
		#endif;
	}

	.loop;
	val = val + 2;
	if (val <= 6) {
		#ifdef OPFORM;
		#ifdef TARGET_ZCODE;
		@jump loop;
		#ifnot;
		@jump ?loop;
		#endif;
		#ifnot;
		jump loop;
		#endif;
	}

	.end;
	return val;
];
